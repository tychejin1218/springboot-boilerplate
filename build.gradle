plugins {
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id 'checkstyle'
    id 'pmd'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

jar {
    enabled(false)
}

bootJar {
    archiveBaseName = 'app'
    archiveFileName = 'app.jar'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Javax Annotation
    implementation 'jakarta.annotation:jakarta.annotation-api'
    testAnnotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // Embedded Tomcat
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'

    // Jasypt
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'

    // Database
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // PostgreSQL
    //implementation 'org.postgresql:postgresql:42.7.3'
    // MySQL
    implementation 'com.mysql:mysql-connector-j:8.4.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-core'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor("com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta")
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // apache common
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-text:1.9'

    // JJWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

test {
    exclude '**/*'
}

clean {
    delete file('src/main/generated')
}

// Checkstyle 설정
checkstyle {
    configFile = file("${rootDir}/config/checkstyle/google-checkstyle.xml")
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/checkstyle-suppressions.xml')]
    ignoreFailures = false
    maxWarnings = 0
    reportsDir = file("${buildDir}/checkstyle-output")
    toolVersion = '10.9.3'
}

checkstyleMain {
    reports {
        xml.outputLocation = file("${checkstyle.reportsDir}/checkstyle-report.xml")
    }
}

// PMD 설정
pmd {
    ruleSetFiles = files("${rootDir}/config/pmd/custom-ruleset.xml")
    ruleSets = []
    consoleOutput = true
    ignoreFailures = false
    reportsDir = file("${buildDir}/pmd-output")
    toolVersion = '6.55.0'
}

pmdMain {
    reports {
        xml.outputLocation = file("${pmd.reportsDir}/pmd-report.xml")
        xml.required = true
    }
}
